/*
 * Project hpwp, 2021-07-13T19:01-0400
 *
 * Copyright 2021 Justin Heflin
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

plugins {
    /* General */
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java-library'
    id 'io.freefair.lombok' version '8.4'
    id "com.github.ben-manes.versions" version "0.49.0"
}

repositories {
    mavenCentral()
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://repo.minebench.de/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven {
        url 'https://repo.codemc.io/repository/maven-snapshots/'
        content {
            includeGroupByRegex 'net\\.wesjd.*'
        }
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
}

allprojects {
    group 'com.heretere'
    version '1.7.4'
    description "Activate plugins in specific worlds."
}

dependencies {
    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'

    compileOnlyApi 'org.jetbrains:annotations:24.1.0'
    compileOnly 'org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT'

    compileOnlyApi 'me.clip:placeholderapi:2.11.5'

    implementation 'org.bstats:bstats-bukkit:3.0.2'

    implementation 'com.github.cryptomorin:XSeries:9.7.0'

    implementation 'de.themoep:inventorygui:1.6.1-SNAPSHOT'

    implementation 'net.wesjd:anvilgui:1.9.1-SNAPSHOT'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* ACF */
    implementation 'co.aikar:taskchain-bukkit:3.7.2'
    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
}

task replaceSources(type: Sync) {
    inputs.property 'version', version
    from sourceSets.main.java
    into layout.getBuildDirectory().get().dir('src')
    filter { line -> line.replaceAll('VERSION', version) }
    filter { line -> line.replaceAll('DISTRIBUTION', 'self-compiled') }
}

compileJava {
    source = replaceSources.outputs
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

shadowJar {
    archiveFileName = 'HPWP.jar'
    relocate 'com.cryptomorin.xseries', 'com.heretere.hpwp.libs.xseries'
    relocate 'de.themoep.inventorygui', 'com.heretere.hpwp.libs.inventory'
    relocate 'com.heretere.hch', 'com.heretere.hpwp.libs.hch'
    relocate 'co.aikar.taskchain', 'com.heretere.hpwp.libs.taskchain'
    relocate 'co.aikar.commands', 'com.heretere.hpwp.libs.acf'
    relocate 'co.aikar.locales', 'com.heretere.hpwp.libs.acf.locales'
    relocate 'org.bstats', 'com.heretere.hpwp.libs.bstats'
    relocate 'net.wesjd.anvilgui', 'com.heretere.hpwp.libs.anvilgui'
    minimize {
        exclude(dependency('net.wesjd:anvilgui:.*'))
    }
}
